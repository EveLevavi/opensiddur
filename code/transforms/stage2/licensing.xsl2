<?xml version="1.0" encoding="UTF-8"?>
<xsl:stylesheet
  version="2.0"
  xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
  xmlns:tei="http://www.tei-c.org/ns/1.0"
  xmlns:j="http://jewishliturgy.org/ns/jlptei/1.0"
  xmlns:jx="http://jewishliturgy.org/ns/jlp-processor"
  xmlns:func="http://jewishliturgy.org/ns/functions/xslt"
  xmlns:rdf="http://www.w3.org/1999/02/22-rdf-syntax-ns#"
  xmlns:cc="http://web.resource.org/cc/"
  xmlns:xs="http://www.w3.org/2001/XMLSchema"
  xmlns:xd="http://www.pnp-software.com/XSLTdoc"
  exclude-result-prefixes="#all"
  >
  <xsl:import href="../../common/common.xsl2"/>
  <xd:doc type="stylesheet">
    <xd:short>Given a completed document, and all its sources 
    (jx:document-source), find all unique licensing statements
    and apply templates to them.</xd:short>
    <xd:copyright>Copyright 2010 Efraim Feinstein (LGPL 3+)</xd:copyright>
    <xd:detail>
        <p>This program is free software: you can redistribute it and/or modify
        it under the terms of the GNU Lesser General Public License as published by
        the Free Software Foundation, either version 3 of the License, or
        (at your option) any later version.</p>

        <p>This program is distributed in the hope that it will be useful,
        but WITHOUT ANY WARRANTY; without even the implied warranty of
        MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
        GNU Lesser General Public License for more details.</p>

        <p>You should have received a copy of the GNU Lesser General Public License
        along with this program.  If not, see 
        <a href="http://www.gnu.org/licenses/">http://www.gnu.org/licenses/</a>.
        </p>
      </xd:detail>
      <xd:svnId>$Id: licensing.xsl2 687 2011-01-23 23:36:48Z efraim.feinstein $</xd:svnId>
  </xd:doc>

  <xsl:variable name="string-table" as="document-node()"
    select="doc('strings.xml')"/>

  <xd:doc>Language code for primary document language</xd:doc>
  <xsl:param name="primary-language" select="'en'" as="xs:string"/>

  <xd:doc>
    <xd:short>Generate a license list</xd:short>
  </xd:doc>
  <xsl:template match="tei:TEI" mode="licensing">
    <xsl:variable name="all-license-statements"
      select="for $uri in distinct-values(//jx:document-source)
        return doc($uri)//tei:availability"
      as="element(tei:availability)*"/>
      
    <xsl:variable name="distinct-legalcode" as="xs:string*"
      select="distinct-values($all-license-statements//
        cc:legalcode/@rdf:resource)"
    />
    <xsl:sequence select="func:debug($distinct-legalcode, 'distinct-legalcode', $debug-info)"/>
    <xsl:variable name="unique-licenses" as="element(tei:availability)*"
      select="for $legalcode in $distinct-legalcode return
        $all-license-statements[descendant::cc:legalcode/@rdf:resource=$legalcode][1]"/>
    <xsl:sequence select="func:debug($unique-licenses, 'unique-licenses', $debug-info)"/>
    <xsl:variable name="additional-statements"
      as="element(tei:p)*"
      select="$all-license-statements//tei:p[@n='additional']"
      />
    <xsl:variable name="additional-attributions"
      as="element(tei:p)*"
      select="$all-license-statements//tei:p[@n='attribution']"
      />
    
    <xsl:variable name="licensing-statements" as="element(tei:div)">
      <tei:div type="licenses">
        <tei:head>
          <xsl:sequence 
            select="func:get-string($string-table,'licensing-header', 
              $primary-language)"/>
        </tei:head>
        <tei:p>
          <xsl:sequence
            select="func:get-string($string-table,'licensing-statement', 
              $primary-language)"/>
        </tei:p>
        <xsl:sequence select="$unique-licenses"/>
        
        <xsl:if test="$additional-statements">
          <tei:p>
            <xsl:sequence
              select="func:get-string($string-table,'licensing-additional', 
              $primary-language)"/>
          </tei:p>
          <xsl:sequence select="$additional-statements"/>
        </xsl:if>
        <xsl:if test="$additional-attributions">
          <tei:p>
            <xsl:sequence
              select="func:get-string($string-table,'licensing-attribution', 
              $primary-language)"/>
          </tei:p>
          <xsl:sequence select="$additional-attributions"/>
        </xsl:if>
      </tei:div>
    </xsl:variable>
    <xsl:apply-templates select="$licensing-statements"/>
  </xsl:template>  
  
  
</xsl:stylesheet>