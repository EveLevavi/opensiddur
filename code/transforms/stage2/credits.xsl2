<?xml version="1.0" encoding="UTF-8"?>
<xsl:stylesheet
  version="2.0"
  xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
  xmlns:tei="http://www.tei-c.org/ns/1.0"
  xmlns:j="http://jewishliturgy.org/ns/jlptei/1.0"
  xmlns:jx="http://jewishliturgy.org/ns/jlp-processor"
  xmlns:func="http://jewishliturgy.org/ns/functions/xslt"
  xmlns:rdf="http://www.w3.org/1999/02/22-rdf-syntax-ns#"
  xmlns:cc="http://web.resource.org/cc/"
  xmlns:xs="http://www.w3.org/2001/XMLSchema"
  xmlns:xd="http://www.pnp-software.com/XSLTdoc"
  exclude-result-prefixes="#all"
  >
  
  <xsl:import href="../../common/common.xsl2"/>
  <xsl:import href="../follow-uri.xsl2"/>
  <xsl:include href="../stage1/ptr.xsl2"/>
  <xd:doc type="stylesheet">
    <xd:short>Given a completed document, and all its credit attributions 
    (jx:credits), find all unique contributors and apply templates to them.
    </xd:short>
    <xd:copyright>Copyright 2010 Efraim Feinstein (LGPL 3+)</xd:copyright>
    <xd:detail>
        <p>This program is free software: you can redistribute it and/or modify
        it under the terms of the GNU Lesser General Public License as published by
        the Free Software Foundation, either version 3 of the License, or
        (at your option) any later version.</p>

        <p>This program is distributed in the hope that it will be useful,
        but WITHOUT ANY WARRANTY; without even the implied warranty of
        MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
        GNU Lesser General Public License for more details.</p>

        <p>You should have received a copy of the GNU Lesser General Public License
        along with this program.  If not, see 
        <a href="http://www.gnu.org/licenses/">http://www.gnu.org/licenses/</a>.
        </p>
      </xd:detail>
      <xd:svnId>$Id: credits.xsl2 687 2011-01-23 23:36:48Z efraim.feinstein $</xd:svnId>
  </xd:doc>

  <xsl:variable name="credits-string-table" as="document-node()">
    <xsl:document>
      <jx:string n="contributors-header" xml:lang="en">Contributors</jx:string>
      <jx:string n="contributors-statement" xml:lang="en">
      We would like to acknowledge the following people and organizations for 
      contributing material:</jx:string>
      <jx:string n="encoder" xml:lang="en">Encoders</jx:string>
      <jx:string n="transcriber" xml:lang="en">Transcribers</jx:string>
      <jx:string n="scanner" xml:lang="en">Scanners</jx:string>
      <jx:string n="author" xml:lang="en">Authors</jx:string>
      <jx:string n="editor" xml:lang="en">Editors</jx:string>
    </xsl:document>
  </xsl:variable>
<!-- 
  <xd:doc>Language code for primary document language</xd:doc>
  <xsl:param name="primary-language" select="'en'" as="xs:string"/>
 -->
  <xd:doc>
    <xd:short>Generate a credits list, under tei:div[@type='credits']</xd:short>
  </xd:doc>
  <xsl:template match="tei:TEI" mode="credits" as="element(tei:div)?">
    <xsl:variable name="all-credits"
      select="//jx:credit"
      as="element(jx:credit)*"/>
    
    <xsl:variable name="contributors" as="element(tei:list)*">
      <xsl:for-each-group select="$all-credits" group-by="jx:role">
        <tei:list type="credits">
          <tei:head><xsl:sequence select="$credits-string-table/jx:string
            [@n=current-grouping-key()
             and lang($primary-language)]/node()"/></tei:head>
          <xsl:for-each-group select="current-group()"
            group-by="jx:contributor">
            <xsl:sequence select="func:debug(current-group(), ('tei:TEI, m=credits, group=', string(position())), $debug-info)"/>
            <xsl:for-each select="current-group()[1]">
              <xsl:apply-templates 
                select="func:follow-uri(jx:contributor,., -1)"/>
            </xsl:for-each>
          </xsl:for-each-group>
        </tei:list>
      </xsl:for-each-group>
    </xsl:variable>
    <xsl:if test="$contributors">
      <tei:div type="credits">
        <tei:head>
          <xsl:sequence 
            select="$credits-string-table/jx:string[@n='contributors-header' 
              and lang($primary-language)]/node()"/>
        </tei:head>
        <xsl:sequence select="$contributors"/>
      </tei:div>
    </xsl:if>
    
  </xsl:template>  
  
</xsl:stylesheet>